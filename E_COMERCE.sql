

-- PROJETO E-COMMERCE:


USE EMPRESA_X
GO

-- TABELAS 

-- TABELA CLIENTE
CREATE TABLE CLIENTE(
	IDCLIENTE INT PRIMARY KEY IDENTITY,
	NOME VARCHAR(50) NOT NULL,
	SEXO CHAR(1) NOT NULL,
	EMAIL VARCHAR(50) NOT NULL UNIQUE,
	TELEFONE VARCHAR(15) NOT NULL UNIQUE,
	DATA_CADASTRO DATETIME DEFAULT GETDATE()
)
ON GA_CLIENTE;
GO

-- TABELA ENDERECO
CREATE TABLE ENDERECO(
	IDENDERECO INT PRIMARY KEY IDENTITY,
	ID_CLIENTE INT NOT NULL,
	RUA VARCHAR(50),
	BAIRRO VARCHAR(50),
	CIDADE VARCHAR(50),
	UF CHAR(2),

	CONSTRAINT FK_ENDERECO_CLIENTE
		FOREIGN KEY (ID_CLIENTE) REFERENCES CLIENTE(IDCLIENTE),

	CONSTRAINT CK_ESTADO_UF CHECK (UF IN (
		'AC','AL','AP','AM','BA','CE','DF','ES','GO','MA',
		'MT','MS','MG','PA','PB','PR','PE','PI','RJ','RN',
		'RS','RO','RR','SC','SP','SE','TO'
	))
)
ON GA_CLIENTE;
GO

-- TABELA TELEFONE
CREATE TABLE TELEFONE(
	IDTELEFONE INT PRIMARY KEY IDENTITY,
	ID_CLIENTE INT NOT NULL,
	TIPO CHAR(3),
	NUMERO VARCHAR(15) NOT NULL UNIQUE,

	CONSTRAINT FK_TELEFONE_CLIENTE
		FOREIGN KEY (ID_CLIENTE) REFERENCES CLIENTE(IDCLIENTE)
)
ON GA_CLIENTE;
GO

-- TABELA CATALOGO
CREATE TABLE CATALOGO(
	IDCATALOGO INT PRIMARY KEY IDENTITY,
	NOME VARCHAR(50) NOT NULL
)
ON GA_CATALOGO;
GO

-- TABELA PRODUTO
CREATE TABLE PRODUTO(
	IDPRODUTO INT PRIMARY KEY IDENTITY,
	NOME VARCHAR(50),
	PRECO DECIMAL(10, 2),
	ID_CATALOGO INT NOT NULL,

	CONSTRAINT FK_PRODUTO_CATALOGO
		FOREIGN KEY (ID_CATALOGO)
		REFERENCES CATALOGO(IDCATALOGO)
)
ON GA_PRODUTO;
GO

-- TABELA VENDEDOR
CREATE TABLE VENDEDOR(
	IDVENDEDOR INT PRIMARY KEY IDENTITY,
	NOME VARCHAR(50),
	SEXO CHAR(1),
	EMAIL VARCHAR(50) UNIQUE,
	TELEFONE VARCHAR(15) UNIQUE
)
ON GA_VENDEDORES;
GO

-- TABELA COMERCIAL (FATO)
CREATE TABLE COMERCIAL(
	IDCOMERCIO INT PRIMARY KEY IDENTITY,
	ID_CLIENTE INT,
	ID_PRODUTO INT,
	ID_VENDEDOR INT,
	DATA_VENDA DATETIME DEFAULT GETDATE(),
	QUANTIDADE INT NOT NULL DEFAULT 1,

	CONSTRAINT FK_COMERCIAL_CLIENTE FOREIGN KEY (ID_CLIENTE) REFERENCES CLIENTE(IDCLIENTE),
	CONSTRAINT FK_COMERCIAL_PRODUTO FOREIGN KEY (ID_PRODUTO) REFERENCES PRODUTO(IDPRODUTO),
	CONSTRAINT FK_COMERCIAL_VENDEDOR FOREIGN KEY (ID_VENDEDOR) REFERENCES VENDEDOR(IDVENDEDOR)
)
ON GA_COMERCIO;
GO

-- TABELA AUDITORIA_GERAL
CREATE TABLE AUDITORIA_GERAL(
	ID_AUDIT INT PRIMARY KEY IDENTITY,
	TABELA VARCHAR(50),
	ID_REGISTRO VARCHAR(50),
	CAMPO VARCHAR(100),
	VALOR_ANTIGO VARCHAR(500),
	VALOR_NOVO VARCHAR(500),
	TIPO_OPERACAO VARCHAR(10),
	DATA_OPERACAO DATETIME2 DEFAULT SYSDATETIME()
)
ON GA_AUDITORIA_GERAL;
GO

-- TRIGGERS DE AUDITORIA

-- CLIENTE
CREATE TRIGGER TRG_CLIENTE_INSERT ON CLIENTE AFTER INSERT AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'CLIENTE', CONVERT(VARCHAR(50), I.IDCLIENTE), 'TODOS', CONCAT('NOME=', I.NOME,',', 'SEXO=', I.SEXO,',', 'EMAIL=', I.EMAIL,',', 'TELEFONE=', I.TELEFONE,',', 'DATA_CADASTRO=', CONVERT(VARCHAR(50), I.DATA_CADASTRO, 120)), 'INSERT'
	FROM inserted I;
END
GO

CREATE TRIGGER TRG_CLIENTE_UPADATE ON CLIENTE AFTER UPDATE AS
BEGIN
	SET NOCOUNT ON;
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'CLIENTE', CONVERT(VARCHAR(50), d.IDCLIENTE), Changes.Campo, Changes.ValorAntigo, Changes.ValorNovo, 'UPDATE'
	FROM deleted d
	INNER JOIN inserted i ON d.IDCLIENTE = i.IDCLIENTE
	CROSS APPLY(
		VALUES ('NOME', d.NOME, i.NOME), ('SEXO', d.SEXO, i.SEXO), ('EMAIL', d.EMAIL, i.EMAIL), ('TELEFONE', d.TELEFONE, i.TELEFONE), ('DATA_CADASTRO', CONVERT(VARCHAR(250), d.DATA_CADASTRO, 120), CONVERT(VARCHAR(250), i.DATA_CADASTRO, 120))
	) AS Changes(Campo, ValorAntigo, ValorNovo)
	WHERE (Changes.ValorAntigo <> Changes.ValorNovo) OR (Changes.ValorAntigo IS NULL AND Changes.ValorNovo IS NOT NULL) OR (Changes.ValorAntigo IS NOT NULL AND Changes.ValorNovo IS NULL);
END
GO

CREATE TRIGGER TRG_CLIENTE_DELETE ON CLIENTE AFTER DELETE AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, TIPO_OPERACAO)
	SELECT 'CLIENTE', CONVERT(VARCHAR(50), d.IDCLIENTE), 'TODOS', CONCAT('NOME=', d.NOME,',', 'SEXO=', d.SEXO,',', 'EMAIL=', d.EMAIL,',', 'TELEFONE=', d.TELEFONE,',', 'DATA_CADASTRO=', CONVERT(VARCHAR(50), d.DATA_CADASTRO, 120)), 'DELETE'
	FROM deleted d;
END
GO

-- ENDERECO
CREATE TRIGGER TRG_ENDERECO_INSERT ON ENDERECO AFTER INSERT AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'ENDERECO', CONVERT(VARCHAR(50), i.IDENDERECO), 'TODOS', CONCAT('ID_CLIENTE=', CONVERT(VARCHAR(50), i.ID_CLIENTE),',', 'RUA=', i.RUA,',', 'BAIRRO=', i.BAIRRO,',', 'CIDADE=', i.CIDADE,',', 'UF=', i.UF), 'INSERT'
	FROM inserted i;
END
GO

CREATE TRIGGER TRG_ENDERECO_UPDATE ON ENDERECO AFTER UPDATE AS
BEGIN
	SET NOCOUNT ON;
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'ENDERECO', CONVERT(VARCHAR(50), d.IDENDERECO), Changes.Campo, CONVERT(VARCHAR(500), Changes.ValorAntigo), CONVERT(VARCHAR(500), Changes.ValorNovo), 'UPDATE'
	FROM deleted d
	INNER JOIN inserted i ON d.IDENDERECO =	i.IDENDERECO
	CROSS APPLY ( VALUES ('ID_CLIENTE', CONVERT(VARCHAR(50), d.ID_CLIENTE), CONVERT(VARCHAR(50), i.ID_CLIENTE)), ('RUA', d.RUA, i.RUA), ('BAIRRO', d.BAIRRO, i.BAIRRO), ('CIDADE', d.CIDADE, i.CIDADE), ('UF', d.UF, i.UF) ) AS Changes(Campo, ValorAntigo, ValorNovo)
	WHERE (Changes.ValorAntigo <> Changes.ValorNovo) OR (Changes.ValorAntigo IS NULL AND Changes.ValorNovo IS NOT NULL) OR (Changes.ValorAntigo IS NOT NULL AND Changes.ValorNovo IS NULL);
END
GO

CREATE TRIGGER TRG_ENDERECO_DELETE ON ENDERECO AFTER DELETE AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, TIPO_OPERACAO)
	SELECT 'ENDERECO', CONVERT(VARCHAR(50), d.IDENDERECO), 'TODOS', CONCAT('ID_CLIENTE=', CONVERT(VARCHAR(50), d.ID_CLIENTE), ',', 'RUA=', d.RUA,',', 'BAIRRO=', d.BAIRRO, ',', 'CIDADE=', d.CIDADE,',', 'UF=', d.UF), 'DELETE'
	FROM deleted d;
END
GO

-- TELEFONE
CREATE TRIGGER TRG_TELEFONE_INSERT ON TELEFONE AFTER INSERT AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'TELEFONE', CONVERT(VARCHAR(50), i.IDTELEFONE), 'TODOS', CONCAT('ID_CLIENTE=', CONVERT(VARCHAR(50), i.ID_CLIENTE), ',', 'TIPO=', i.TIPO, ',', 'NUMERO=', i.NUMERO), 'INSERT'
	FROM inserted i;
END
GO

CREATE TRIGGER TRG_TELEFONE_UPDATE ON TELEFONE AFTER UPDATE AS
BEGIN
	SET NOCOUNT ON;
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'TELEFONE', CONVERT(VARCHAR(50), d.IDTELEFONE), Changes.Campo, Changes.ValorAntigo, Changes.ValorNovo, 'UPDATE'
	FROM deleted d
	INNER JOIN inserted i ON d.IDTELEFONE = i.IDTELEFONE
	CROSS APPLY( VALUES ('ID_CLIENTE', CONVERT(VARCHAR(50), d.ID_CLIENTE), CONVERT(VARCHAR(50), i.ID_CLIENTE)), ('TIPO', d.TIPO, i.TIPO), ('NUMERO', d.NUMERO, i.NUMERO) ) AS Changes(Campo, ValorAntigo, ValorNovo)
	WHERE (Changes.ValorAntigo <> Changes.ValorNovo) OR (Changes.ValorAntigo IS NULL AND Changes.ValorNovo IS NOT NULL) OR (Changes.ValorAntigo IS NOT NULL AND Changes.ValorNovo IS NULL);
END
GO

CREATE TRIGGER TRG_TELEFONE_DELETE ON TELEFONE AFTER DELETE AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, TIPO_OPERACAO)
	SELECT 'TELEFONE', CONVERT(VARCHAR(50), d.IDTELEFONE), 'TODOS', CONCAT('ID_CLIENTE=', CONVERT(VARCHAR(50), d.ID_CLIENTE),',', 'TIPO=', d.TIPO,',', 'NUMERO=', d.NUMERO), 'DELETE'
	FROM deleted d;
END
GO

-- CATALOGO
CREATE TRIGGER TRG_CATALOGO_INSERT ON CATALOGO AFTER INSERT AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'CATALOGO', CONVERT(VARCHAR(50), i.IDCATALOGO), 'NOME', i.NOME, 'INSERT'
	FROM inserted i;
END
GO

CREATE TRIGGER TRG_CATALOGO_UPDATE ON CATALOGO AFTER UPDATE AS
BEGIN
	SET NOCOUNT ON;
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'CATALOGO', CONVERT(VARCHAR(50), d.IDCATALOGO), 'NOME', d.NOME, i.NOME, 'UPDATE'
	FROM deleted d
	INNER JOIN inserted i ON d.IDCATALOGO = i.IDCATALOGO
	WHERE d.NOME <> i.NOME;
END
GO

CREATE TRIGGER TRG_CATALOGO_DELETE ON CATALOGO AFTER DELETE AS
BEGIN
	INSERT INTO AUDITORIA_GERAL (TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, TIPO_OPERACAO)
	SELECT 'CATALOGO', CONVERT(VARCHAR(50), d.IDCATALOGO), 'NOME', d.NOME, 'DELETE'
	FROM deleted d;
END
GO

-- PRODUTO
CREATE TRIGGER TRG_PRODUTO_INSERT ON PRODUTO AFTER INSERT AS
BEGIN
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'PRODUTO', CONVERT(VARCHAR(50), i.IDPRODUTO), 'TODOS', CONCAT('NOME=', i.NOME,',', 'PRECO=', CONVERT(VARCHAR(50), i.PRECO),',', 'ID_CATALOGO=', CONVERT(VARCHAR(50), i.ID_CATALOGO)), 'INSERT'
	FROM inserted i;
END
GO

CREATE TRIGGER TRG_PRODUTO_UPDATE ON PRODUTO AFTER UPDATE AS
BEGIN
	SET NOCOUNT ON;
	INSERT INTO AUDITORIA_GERAL(TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'PRODUTO', CONVERT(VARCHAR(50), d.IDPRODUTO), Changes.Campo, Changes.ValorAntigo, Changes.ValorNovo, 'UPDATE'
	FROM deleted d
	INNER JOIN inserted i ON d.IDPRODUTO = i.IDPRODUTO
	CROSS APPLY(
		VALUES ('NOME', d.NOME, i.NOME), ('PRECO', CONVERT(VARCHAR(50), d.PRECO), CONVERT(VARCHAR(50), i.PRECO)), ('ID_CATALOGO', CONVERT(VARCHAR(50), d.ID_CATALOGO), CONVERT(VARCHAR(50), i.ID_CATALOGO))
	) AS Changes(Campo, ValorAntigo, ValorNovo)
	WHERE (Changes.ValorAntigo <> Changes.ValorNovo) OR (Changes.ValorAntigo IS NULL AND Changes.ValorNovo IS NOT NULL) OR (Changes.ValorAntigo IS NOT NULL AND Changes.ValorNovo IS NULL);
END
GO

CREATE TRIGGER TRG_PRODUTO_DELETE ON PRODUTO AFTER DELETE AS
BEGIN
	INSERT INTO AUDITORIA_GERAL (TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, TIPO_OPERACAO)
	SELECT 'PRODUTO', CONVERT(VARCHAR(50), d.IDPRODUTO), 'TODOS', CONCAT('NOME=', d.NOME, ', ', 'PRECO=', CONVERT(VARCHAR(50), d.PRECO), ', ', 'ID_CATALOGO=', CONVERT(VARCHAR(50), d.ID_CATALOGO)), 'DELETE'
	FROM deleted d;
END
GO

-- VENDEDOR
CREATE TRIGGER TRG_VENDEDOR_INSERT ON VENDEDOR AFTER INSERT AS
BEGIN
	INSERT INTO AUDITORIA_GERAL (TABELA, ID_REGISTRO, CAMPO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'VENDEDOR', CONVERT(VARCHAR(50), i.IDVENDEDOR), 'TODOS', CONCAT('NOME=', i.NOME, ', ', 'SEXO=', i.SEXO,',', 'EMAIL=', i.EMAIL,',', 'TELEFONE=', i.TELEFONE), 'INSERT'
	FROM inserted i;
END
GO

CREATE TRIGGER TRG_VENDEDOR_UPDATE ON VENDEDOR AFTER UPDATE AS
BEGIN
	SET NOCOUNT ON;
	INSERT INTO AUDITORIA_GERAL (TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, VALOR_NOVO, TIPO_OPERACAO)
	SELECT 'VENDEDOR', CONVERT(VARCHAR(50), d.IDVENDEDOR), Changes.Campo, Changes.ValorAntigo, Changes.ValorNovo, 'UPDATE'
	FROM deleted d
	INNER JOIN inserted i ON d.IDVENDEDOR = i.IDVENDEDOR
	CROSS APPLY ( VALUES ('NOME', d.NOME, i.NOME), ('SEXO', d.SEXO, i.SEXO), ('EMAIL', d.EMAIL, i.EMAIL), ('TELEFONE', d.TELEFONE, i.TELEFONE) ) AS Changes(Campo, ValorAntigo, ValorNovo)
	WHERE (Changes.ValorAntigo <> Changes.ValorNovo) OR (Changes.ValorAntigo IS NULL AND Changes.ValorNovo IS NOT NULL) OR (Changes.ValorAntigo IS NOT NULL AND Changes.ValorNovo IS NULL);
END
GO

CREATE TRIGGER TRG_VENDEDOR_DELETE ON VENDEDOR AFTER DELETE AS
BEGIN
	INSERT INTO AUDITORIA_GERAL (TABELA, ID_REGISTRO, CAMPO, VALOR_ANTIGO, TIPO_OPERACAO)
	SELECT 'VENDEDOR', CONVERT(VARCHAR(50), d.IDVENDEDOR), 'TODOS', CONCAT('NOME=', d.NOME, ', ', 'SEXO=', d.SEXO, ', ', 'EMAIL=', d.EMAIL, ', ', 'TELEFONE=', d.TELEFONE), 'DELETE'
	FROM deleted d;
END
GO

-- INSERÇÃO DE DADOS (CARGA INICIAL E MASSIVA)

-- DADOS BASE
INSERT INTO CLIENTE (NOME, SEXO, EMAIL, TELEFONE) VALUES
('Ana Silva', 'F', 'ana.silva@gmail.com', '(11)91234-5678'),
('Carlos Alberto', 'M', 'carlos.alberto@hotmail.com', '(11)98765-4321'),
('Mariana Costa', 'F', 'mariana.costa@yahoo.com', '(21)99876-1122'),
('João Pedro', 'M', 'joao.pedro@gmail.com', '(31)98855-3344'),
('Fernanda Lima', 'F', 'fernanda.lima@outlook.com', '(41)99772-8899'),
('Ricardo Santos', 'M', 'ricardo.santos@gmail.com', '(27)99644-2211'),
('Patrícia Ramos', 'F', 'patricia.ramos@gmail.com', '(48)99123-4455'),
('Gabriel Rocha', 'M', 'gabriel.rocha@hotmail.com', '(71)98987-6655'),
('Juliana Martins', 'F', 'juliana.martins@gmail.com', '(62)98655-7788'),
('Eduardo Carvalho', 'M', 'eduardo.carvalho@outlook.com', '(85)98321-9090');

INSERT INTO ENDERECO (ID_CLIENTE, RUA, BAIRRO, CIDADE, UF) VALUES
(1, 'Av Paulista', 'Bela Vista', 'São Paulo', 'SP'),
(2, 'Rua das Flores', 'Centro', 'São Paulo', 'SP'),
(3, 'Av Atlântica', 'Copacabana', 'Rio de Janeiro', 'RJ'),
(4, 'Rua Goiás', 'Savassi', 'Belo Horizonte', 'MG'),
(5, 'Av Batel', 'Batel', 'Curitiba', 'PR'),
(6, 'Rua Vitória', 'Centro', 'Vitória', 'ES'),
(7, 'Rua das Palmeiras', 'Estreito', 'Florianópolis', 'SC'),
(8, 'Rua Barros Reis', 'Liberdade', 'Salvador', 'BA'),
(9, 'Rua 10', 'Setor Bueno', 'Goiânia', 'GO'),
(10, 'Rua Dragão do Mar', 'Meireles', 'Fortaleza', 'CE');

INSERT INTO TELEFONE (ID_CLIENTE, TIPO, NUMERO) VALUES
(1, 'CEL', '11987654321'), (1, 'COM', '1130459080'), (2, 'CEL', '11999887766'),
(3, 'RES', '2125687788'), (4, 'CEL', '31999887766'), (5, 'CEL', '41988776655'),
(6, 'COM', '2732129980'), (7, 'RES', '4832129988'), (8, 'CEL', '71988997766'),
(9, 'CEL', '62988776655'), (10, 'RES', '8532124455');

INSERT INTO CATALOGO (NOME) VALUES
('Proteção Auditiva'), ('Proteção Respiratória'), ('Proteção das Mãos'),
('Proteção dos Olhos e Face'), ('Proteção dos Pés'), ('Proteção Corporal');

INSERT INTO PRODUTO (NOME, PRECO, ID_CATALOGO) VALUES
('Protetor Auricular Plugs 3M', 15.50, 1), ('Abafador de Ruído 3M Peltor', 95.99, 1),
('Máscara PFF2 (N95) 3M', 12.00, 2), ('Máscara Respirador Semi-Facial com Filtro', 150.00, 2),
('Luva de Raspa', 18.75, 3), ('Luva Nitrílica Anticorte', 32.50, 3),
('Luva de Látex para Química', 25.00, 3), ('Óculos de Proteção Incolor', 20.90, 4),
('Protetor Facial com Visor', 45.30, 4), ('Botina de Segurança com Biqueira de Aço', 120.00, 5),
('Sapato de Segurança Antiderrapante', 90.00, 5), ('Avental de Raspa para Soldador', 80.00, 6),
('Colete Refletivo Classe 2', 35.00, 6), ('Capacete de Segurança CA 498', 55.00, 6);

INSERT INTO VENDEDOR (NOME, SEXO, EMAIL, TELEFONE) VALUES
('Vendedor A', 'M', 'vend_a@empresa.com', '(11)91111-1111'),
('Vendedora B', 'F', 'vend_b@empresa.com', '(21)92222-2222'),
('Vendedor C', 'M', 'vend_c@empresa.com', '(31)93333-3333');

INSERT INTO VENDEDOR (NOME, SEXO, EMAIL, TELEFONE) VALUES
('Vendedor D', 'M', 'vend_d@empresa.com', '(31)94444-4444'),
('Vendedor E', 'M', 'vend_e@empresa.com', '(31)95555-5555');


INSERT INTO COMERCIAL (ID_CLIENTE, ID_PRODUTO, ID_VENDEDOR, QUANTIDADE) VALUES
(1, 3, 1, 5), (2, 10, 2, 1), (3, 8, 1, 2), (4, 12, 3, 3), (1, 3, 1, 10);
GO

INSERT INTO COMERCIAL (ID_CLIENTE, ID_PRODUTO, ID_VENDEDOR, QUANTIDADE) VALUES
(5, 4, 4, 2),  
(6, 7, 5, 1); 
GO


DECLARE @ID_CLIENTE_RAND INT = (SELECT MAX(IDCLIENTE) FROM CLIENTE) - 5;
DECLARE @ID_PRODUTO_EX INT = (SELECT MAX(IDPRODUTO) FROM PRODUTO);
DECLARE @DATA_HOJE DATETIME = GETDATE();

INSERT INTO COMERCIAL (ID_CLIENTE, ID_PRODUTO, ID_VENDEDOR, QUANTIDADE, DATA_VENDA) VALUES
(@ID_CLIENTE_RAND, @ID_PRODUTO_EX, 4, 5, DATEADD(MONTH, -2, @DATA_HOJE)); 

INSERT INTO COMERCIAL (ID_CLIENTE, ID_PRODUTO, ID_VENDEDOR, QUANTIDADE, DATA_VENDA) VALUES
(@ID_CLIENTE_RAND + 1, @ID_PRODUTO_EX - 1, 4, 1, @DATA_HOJE);                


INSERT INTO COMERCIAL (ID_CLIENTE, ID_PRODUTO, ID_VENDEDOR, QUANTIDADE, DATA_VENDA) VALUES
(@ID_CLIENTE_RAND + 2, @ID_PRODUTO_EX, 5, 10, DATEADD(MONTH, -3, @DATA_HOJE)); 

INSERT INTO COMERCIAL (ID_CLIENTE, ID_PRODUTO, ID_VENDEDOR, QUANTIDADE, DATA_VENDA) VALUES
(@ID_CLIENTE_RAND + 3, @ID_PRODUTO_EX - 2, 5, 2, @DATA_HOJE);                 
GO

SELECT 
    V.NOME, 
    COUNT(C.IDCOMERCIO) AS NovasVendasInseridas
FROM VENDEDOR V
JOIN COMERCIAL C ON V.IDVENDEDOR = C.ID_VENDEDOR
WHERE V.IDVENDEDOR IN (4, 5)
GROUP BY V.NOME;
GO

-- GERADOR DE DADOS 
SET NOCOUNT ON;
GO

DECLARE @I INT = 11;
DECLARE @TOTAL_CLIENTES INT = 1000;
DECLARE @SEXO CHAR(1);
DECLARE @NOME_BASE VARCHAR(50);
DECLARE @SOBRENOME_BASE VARCHAR(50);
DECLARE @EMAIL_BASE VARCHAR(100);
DECLARE @TELEFONE_BASE VARCHAR(15);
DECLARE @CIDADE_BASE VARCHAR(50);
DECLARE @UF_BASE CHAR(2);
DECLARE @TIPO_TELEFONE CHAR(3);
DECLARE @ID_CLIENTE_NOVO INT;

DECLARE @NOMES TABLE (Nome VARCHAR(50));
INSERT INTO @NOMES VALUES ('Lucas'), ('Maria'), ('Pedro'), ('Juliana'), ('Rafael'), ('Camila'), ('Fernando'), ('Isabela'), ('Guilherme'), ('Amanda'), ('Vinicius'), ('Larissa'), ('Thiago'), ('Beatriz'), ('Felipe'), ('Giovana');

DECLARE @SOBRENOMES TABLE (Sobrenome VARCHAR(50));
INSERT INTO @SOBRENOMES VALUES ('Souza'), ('Oliveira'), ('Pereira'), ('Rodrigues'), ('Almeida'), ('Costa'), ('Santos'), ('Gomes'), ('Martins'), ('Ribeiro'), ('Fernandes'), ('Carvalho'), ('Lima'), ('Barbosa'), ('Rocha');

DECLARE @CIDADES TABLE (Cidade VARCHAR(50), UF CHAR(2));
INSERT INTO @CIDADES VALUES ('Campinas', 'SP'), ('Niterói', 'RJ'), ('Porto Alegre', 'RS'), ('Recife', 'PE'), ('Curitiba', 'PR'), ('Brasília', 'DF'), ('Salvador', 'BA'), ('Goiânia', 'GO'), ('Manaus', 'AM'), ('Belém', 'PA');

WHILE @I <= @TOTAL_CLIENTES
BEGIN
	IF (CAST(RAND() * 10 AS INT) % 2) = 0 SET @SEXO = 'M'; ELSE SET @SEXO = 'F';

	SELECT TOP 1 @NOME_BASE = Nome FROM @NOMES ORDER BY NEWID();
	SELECT TOP 1 @SOBRENOME_BASE = Sobrenome FROM @SOBRENOMES ORDER BY NEWID();

	SET @EMAIL_BASE = LOWER(REPLACE(@NOME_BASE + '.' + @SOBRENOME_BASE + CONVERT(VARCHAR(4), @I), ' ', '')) + '@ficticio.com';
	SET @TELEFONE_BASE = '(99)9' + RIGHT('0000000' + CONVERT(VARCHAR(7), @I), 7) + '-' + RIGHT('0000' + CONVERT(VARCHAR(4), @I), 4);
	SELECT TOP 1 @CIDADE_BASE = Cidade, @UF_BASE = UF FROM @CIDADES ORDER BY NEWID();

	INSERT INTO CLIENTE (NOME, SEXO, EMAIL, TELEFONE) VALUES (@NOME_BASE + ' ' + @SOBRENOME_BASE + ' ' + CONVERT(VARCHAR(4), @I), @SEXO, @EMAIL_BASE, @TELEFONE_BASE);
	SET @ID_CLIENTE_NOVO = SCOPE_IDENTITY();

	INSERT INTO ENDERECO (ID_CLIENTE, RUA, BAIRRO, CIDADE, UF) VALUES (@ID_CLIENTE_NOVO, 'Rua Fictícia ' + CONVERT(VARCHAR(10), @I), 'Bairro X', @CIDADE_BASE, @UF_BASE);

	IF (CAST(RAND() * 10 AS INT) % 2) = 0 SET @TIPO_TELEFONE = 'CEL'; ELSE SET @TIPO_TELEFONE = 'RES';
	INSERT INTO TELEFONE (ID_CLIENTE, TIPO, NUMERO) VALUES (@ID_CLIENTE_NOVO, @TIPO_TELEFONE, '9' + RIGHT('0000000' + CONVERT(VARCHAR(7), @I * 10), 8));

	SET @I = @I + 1;
END
GO

-- GERADOR DE TRANSAÇÕES 

DECLARE @I INT = 1;
DECLARE @TOTAL_VENDAS INT = 5000;
DECLARE @CLIENTE_MIN_ID INT;
DECLARE @CLIENTE_MAX_ID INT;
DECLARE @PRODUTO_MIN_ID INT;
DECLARE @PRODUTO_MAX_ID INT;
DECLARE @VENDEDOR_MIN_ID INT;
DECLARE @VENDEDOR_MAX_ID INT;
DECLARE @ID_CLIENTE_RAND INT;
DECLARE @ID_PRODUTO_RAND INT;
DECLARE @ID_VENDEDOR_RAND INT;
DECLARE @QUANTIDADE_RAND INT;
DECLARE @DATA_VENDA_RAND DATETIME;
DECLARE @RAND_SELECTOR DECIMAL(10, 2); 


SELECT @CLIENTE_MIN_ID = MIN(IDCLIENTE), @CLIENTE_MAX_ID = MAX(IDCLIENTE) FROM CLIENTE;
SELECT @PRODUTO_MIN_ID = MIN(IDPRODUTO), @PRODUTO_MAX_ID = MAX(IDPRODUTO) FROM PRODUTO;
SELECT @VENDEDOR_MIN_ID = MIN(IDVENDEDOR), @VENDEDOR_MAX_ID = MAX(IDVENDEDOR) FROM VENDEDOR;


WHILE @I <= @TOTAL_VENDAS
BEGIN
    SET @ID_CLIENTE_RAND = ROUND(((@CLIENTE_MAX_ID - @CLIENTE_MIN_ID) * RAND()) + @CLIENTE_MIN_ID, 0);
    SET @ID_PRODUTO_RAND = ROUND(((@PRODUTO_MAX_ID - @PRODUTO_MIN_ID) * RAND()) + @PRODUTO_MIN_ID, 0);
    SET @QUANTIDADE_RAND = ROUND(((10 - 1) * RAND()) + 1, 0);
    SET @DATA_VENDA_RAND = DATEADD(DAY, (RAND() * -365), GETDATE());


    SET @RAND_SELECTOR = RAND();
    
    SET @ID_VENDEDOR_RAND = 
        CASE
            WHEN @RAND_SELECTOR <= 0.35 THEN 1  
            WHEN @RAND_SELECTOR <= 0.60 THEN 3  
            WHEN @RAND_SELECTOR <= 0.80 THEN 4  
            WHEN @RAND_SELECTOR <= 0.90 THEN 5  
            ELSE 2                              
        END;

    INSERT INTO COMERCIAL (ID_CLIENTE, ID_PRODUTO, ID_VENDEDOR, DATA_VENDA, QUANTIDADE)
    VALUES (@ID_CLIENTE_RAND, @ID_PRODUTO_RAND, @ID_VENDEDOR_RAND, @DATA_VENDA_RAND, @QUANTIDADE_RAND);

	SET @I = @I + 1;
END
GO

SELECT COUNT(*) AS TotalClientesGeral FROM CLIENTE;
SELECT COUNT(*) AS TotalTransacoesGeral FROM COMERCIAL;
GO

-- OTIMIZAÇÃO DE PERFORMANCE 

CREATE NONCLUSTERED INDEX IX_COMERCIAL_CLIENTE ON COMERCIAL(ID_CLIENTE);
CREATE NONCLUSTERED INDEX IX_COMERCIAL_PRODUTO ON COMERCIAL(ID_PRODUTO);
CREATE NONCLUSTERED INDEX IX_COMERCIAL_VENDEDOR ON COMERCIAL(ID_VENDEDOR);
CREATE NONCLUSTERED INDEX IX_COMERCIAL_DATA ON COMERCIAL(DATA_VENDA);
CREATE NONCLUSTERED INDEX IX_ENDERECO_CLIENTE ON ENDERECO(ID_CLIENTE);
GO





-- VIEWS DE BI 

-- VW_CLIENTES 
CREATE VIEW VW_CLIENTES AS
SELECT
	C.IDCLIENTE,
	C.NOME AS NomeCliente,
	C.SEXO,
	C.EMAIL AS EmailPrincipal,
	C.TELEFONE AS TelefonePrincipal,
	C.DATA_CADASTRO,
	E.RUA,
	E.BAIRRO,
	E.CIDADE,
	E.UF,
	T.TIPO AS TipoTelefoneExtra,
	T.NUMERO AS TelefoneExtra
FROM CLIENTE C
LEFT JOIN ENDERECO E ON C.IDCLIENTE = E.ID_CLIENTE
LEFT JOIN TELEFONE T ON C.IDCLIENTE = T.ID_CLIENTE;
GO

-- VW_FATURAMENTO_ANUAL 
CREATE VIEW VW_FATURAMENTO_ANUAL AS
SELECT
	YEAR(C.DATA_VENDA) AS AnoVenda,
	COUNT(C.IDCOMERCIO) AS TotalTransacoes,
	ISNULL(SUM(C.QUANTIDADE), 0) AS TotalUnidadesVendidas,
	ISNULL(SUM(C.QUANTIDADE * P.PRECO), 0) AS ReceitaTotalAnual
FROM COMERCIAL C
JOIN PRODUTO P ON C.ID_PRODUTO = P.IDPRODUTO
GROUP BY YEAR(C.DATA_VENDA);
GO

-- VW_PERFORMANCE_VENDEDOR
CREATE VIEW VW_PERFORMANCE_VENDEDOR AS
SELECT
	V.IDVENDEDOR,
	V.NOME AS NomeVendedor,
	YEAR(C.DATA_VENDA) AS AnoVenda,
	COUNT(C.IDCOMERCIO) AS TotalTransacoes,
	ISNULL(SUM(C.QUANTIDADE), 0) AS TotalUnidadesVendidas,
	ISNULL(SUM(C.QUANTIDADE * P.PRECO), 0) AS ReceitaGerada
FROM COMERCIAL C
JOIN VENDEDOR V ON C.ID_VENDEDOR = V.IDVENDEDOR
JOIN PRODUTO P ON C.ID_PRODUTO = P.IDPRODUTO
GROUP BY V.IDVENDEDOR, V.NOME, YEAR(C.DATA_VENDA);
GO

-- VW_VENDAS_PRODUTO_CATALOGO
CREATE VIEW VW_VENDAS_PRODUTO_CATALOGO AS
SELECT
	CA.NOME AS NomeCatalogo,
	P.IDPRODUTO,
	P.NOME AS NomeProduto,
	YEAR(C.DATA_VENDA) AS AnoVenda,
	ISNULL(SUM(C.QUANTIDADE), 0) AS VolumeVendas,
	ISNULL(SUM(C.QUANTIDADE * P.PRECO), 0) AS ReceitaProduto
FROM COMERCIAL C
JOIN PRODUTO P ON C.ID_PRODUTO = P.IDPRODUTO
JOIN CATALOGO CA ON P.ID_CATALOGO = CA.IDCATALOGO
GROUP BY CA.NOME, P.IDPRODUTO, P.NOME, YEAR(C.DATA_VENDA);
GO

-- VW_VENDAS_POR_REGIAO
CREATE VIEW VW_VENDAS_POR_REGIAO AS
SELECT
	E.UF,
	E.CIDADE,
	YEAR(C.DATA_VENDA) AS AnoVenda,
	COUNT(DISTINCT C.ID_CLIENTE) AS TotalClientesCompradores,
	COUNT(C.IDCOMERCIO) AS TotalTransacoes,
	ISNULL(SUM(C.QUANTIDADE * P.PRECO), 0) AS ReceitaRegional
FROM COMERCIAL C
JOIN PRODUTO P ON C.ID_PRODUTO = P.IDPRODUTO
JOIN CLIENTE CL ON C.ID_CLIENTE = CL.IDCLIENTE
JOIN ENDERECO E ON CL.IDCLIENTE = E.ID_CLIENTE
GROUP BY E.UF, E.CIDADE, YEAR(C.DATA_VENDA);
GO

-- VW_RENTABILIDADE_TRANSACAO
CREATE VIEW VW_RENTABILIDADE_TRANSACAO AS
SELECT
	C.IDCOMERCIO,
	CL.NOME AS NomeCliente,
	V.NOME AS NomeVendedor,
	C.DATA_VENDA,
	C.QUANTIDADE,
	P.PRECO AS PrecoUnitario,
	(C.QUANTIDADE * P.PRECO) AS ValorTotalVenda,

	CASE WHEN C.QUANTIDADE > 0 THEN (C.QUANTIDADE * P.PRECO) / C.QUANTIDADE ELSE 0 END AS TicketMedioTransacao
FROM COMERCIAL C
JOIN PRODUTO P ON C.ID_PRODUTO = P.IDPRODUTO
JOIN CLIENTE CL ON C.ID_CLIENTE = CL.IDCLIENTE
JOIN VENDEDOR V ON C.ID_VENDEDOR = V.IDVENDEDOR;
GO

-- VW_RECORRENCIA_CLIENTE
CREATE VIEW VW_RECORRENCIA_CLIENTE AS
SELECT
	CL.IDCLIENTE,
	CL.NOME AS NomeCliente,
	YEAR(C.DATA_VENDA) AS AnoVenda,
	COUNT(C.IDCOMERCIO) AS TotalTransacoes,
	ISNULL(SUM(C.QUANTIDADE), 0) AS TotalUnidadesCompradas,
	ISNULL(SUM(C.QUANTIDADE * P.PRECO), 0) AS ValorTotalGasto
FROM COMERCIAL C
JOIN CLIENTE CL ON C.ID_CLIENTE = CL.IDCLIENTE
JOIN PRODUTO P ON C.ID_PRODUTO = P.IDPRODUTO
GROUP BY CL.IDCLIENTE, CL.NOME, YEAR(C.DATA_VENDA);
GO

-- VW_RECORRENCIA_GEOGRAFICA
CREATE VIEW VW_RECORRENCIA_GEOGRAFICA AS
SELECT
	R.IDCLIENTE,
	R.NomeCliente,
	R.AnoVenda,
	R.TotalTransacoes,
	R.ValorTotalGasto,
	E.UF,
	E.CIDADE
FROM VW_RECORRENCIA_CLIENTE R
JOIN ENDERECO E ON R.IDCLIENTE = E.ID_CLIENTE;
GO

-- VW_DASHBOARD_RESUMO 
CREATE VIEW VW_DASHBOARD_RESUMO AS
SELECT
	(SELECT COUNT(*) FROM CLIENTE) AS TotalClientes,
	(SELECT COUNT(*) FROM COMERCIAL) AS TotalVendasRealizadas,
	(SELECT ISNULL(SUM(C.QUANTIDADE * P.PRECO), 0) FROM COMERCIAL C JOIN PRODUTO P ON C.ID_PRODUTO = P.IDPRODUTO) AS FaturamentoTotalHistorico;
GO

